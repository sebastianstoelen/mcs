vocabulary V {
        type index isa int
        queen(index, index) // queen(x, y) is true iff 
    						//there is a queen on row x, column y.
        n : index
        type diag isa int
        diag1(index, index) : diag // These functions map locations on the board
        diag2(index, index) : diag // to their diagonals
}

theory T : V {
        { diag1(x, y) = x - y + n. }
        { diag2(x, y) = x + y - 1. }
    
    // There is exactly one queen in every row
		! r [index]: #{c: queen(r,c)} = 1.
    	//Without aggregate: !x[index] : ?=1y[index] : queen(x, y). 
    
    // There is exactly one queen in every column
		! c [index]: #{r: queen(r,c)} = 1.
    // There can not be more than one queen on any diagonal (2 constraints)
    	! d [diag]: #{r c: diag1(r,c) = d & queen(r,c)} =< 1.
		! d [diag]: #{r c: diag2(r,c) = d & queen(r,c)} =< 1.
}


include "nqueens_solution.aidp"
include "nqueens_draw.idp"
include "nqueens_instance_1.idp"
//include "nqueens_instance_2.idp"
//include "nqueens_instance_3.idp"
include "idpd3/run.idp"