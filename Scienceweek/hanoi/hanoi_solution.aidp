
theory T_exp : V {

	// At the end, all discs are on the right
	! s : onPosition(end,s,right).

	// A disc can only be moved when no discs are on the disc.
	! t s : ~onTopOf(t,s,moveDisc(t)).

	// Er mag nooit een grotere disc op een kleinere liggen.
	! t s1 s2 : s1 > s2 => ~onTopOf(t,s1,s2).

	// Express how the position of disc changes over time.
	{ onPosition(0,disc,left).
	  onPosition(t+1,moveDisc(t),moveTo(t)) <- Time(t).
	  onPosition(t+1,disc,pos) <- onPosition(t,disc,pos) & disc ~= moveDisc(t).
	  onPosition(t+1,disc,pos) <- onPosition(t,disc,pos) & pos = moveTo(t).  }

	// Express how the relative location of discs change over time.
	{ onTopOf(0,x,x+1).
	  onTopOf(t+1,x,y) <- onTopOf(t,x,y) & x ~= moveDisc(t) & y ~= moveDisc(t).
	  onTopOf(t+1,x,y) <- x = moveDisc(t) & p = moveTo(t) & x ~= y
						 & onPosition(t,y,p) & ! z : onTopOf(t,z,y) => x = z. 
	  onTopOf(t+1,x,z) <- onTopOf(t,x,y) & y = moveDisc(t) & onTopOf(t,y,z).
	}
}

procedure name(){
	return("hanoi")
}